class BackboneLeaflet.Custom.Map
  _.extend @prototype, Backbone.Events

  zoom: 15

  constructor: (latitude, longitude, options={})->
    @_buildMap(latitude, longitude)
    @_setHomeMarker(latitude, longitude) if options.home_marker?

  setView: (latitude, longitude, options={})->
    @base.setView([latitude, longitude], @zoom)
    @_setHomeMarker(latitude, longitude) if options.home_marker?

  addMarker: (latitude, longitude, icon=null)->
    icon ?= @_baseIcon()
    marker = L.marker([latitude, longitude], {icon: icon, draggable: true})
    @base.addLayer(marker)
    marker

  removeMarker: (marker)->
    @base.removeLayer(marker)

  bindPopup: (marker, content)->
    marker.bindPopup(content)

  _buildMap: (latitude, longitude)->
    @base = L.map('map').setView([latitude, longitude], @zoom)
    L.tileLayer('http://{s}.tile.cloudmade.com/ceda08740383477f8dbb3bed2e9c2a47/997/256/{z}/{x}/{y}.png',
      attribution: 'Map data &copy; <a href="http://openstreetmap.org">OpenStreetMap</a> contributors, <a href="http://creativecommons.org/licenses/by-sa/2.0/">CC-BY-SA</a>, Imagery Â© <a href="http://cloudmade.com">CloudMade</a>'
      maxZoom: 18).addTo(@base)

  _setHomeMarker: (latitude, longitude)->
    @removeMarker(@current_location) if @current_location?
    @current_location = @addMarker(latitude, longitude)
    @current_location.addEventListener('dragend', @_onHomeDrag)

  _baseIcon: ->
    return @_base_icon if @_base_icon?
    @_base_icon = L.icon(
      iconUrl: "<%= asset_path('marker-icon.png') %>"
      retinaUrl: "<%= asset_path('marker-icon-2x.png') %>"
      shadowUrl: "<%= asset_path('marker-shadow.png') %>"
      iconSize: [25, 41]
      iconAnchor: [12, 41]
      popupAnchor: [1, -34]
      shadowSize: [41, 41])

  _onHomeDrag: (event)=>
    # console.log  "#{key} - #{value}" for key, value of event.target
    marker = event.target
    lat_lng = [marker.getLatLng().lat, marker.getLatLng().lng]
    BackboneLeaflet.Events.trigger("homeDragged", marker.getLatLng().lat, marker.getLatLng().lng)